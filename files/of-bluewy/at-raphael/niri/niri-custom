#!/usr/bin/env nu

# Integrates Niri with Systemd.

def main [
]: nothing -> nothing { }

# Starts niri in a custom way.
def 'main start' [
]: nothing -> nothing {
   systemctl --user start (get-wm-systemd-service-name)
}

# Stops niri.
def 'main stop' [
]: nothing -> nothing {
   systemctl --user stop (get-wm-systemd-service-name)
}

# Saves previous state and imports evironment to systemd (systemd only).
def 'main set-environment' [
]: nothing -> nothing {
   let script_runtime_dir = get-script-runtime-dir

   let script_runtime_dir = get-script-runtime-dir
   mkdir $script_runtime_dir

   let systemd_pre_env = systemctl --user show-environment
   | lines
   | split column '=' key value

   let systemd_pre_env_abs_file_path = get-systemd-pre-env-abs-file-path
   $systemd_pre_env | to json | save -f $systemd_pre_env_abs_file_path

   let wm_env = get-wm-env
   let wm_env_names = $wm_env | columns

   if ($wm_env_names | is-empty) {
      return
   }

   with-env $wm_env {
      systemctl --user import-environment ...$wm_env_names
      dbus-update-activation-environment ...$wm_env_names
   }
}

# Unsets previously setted environment.
def 'main unset-environment' []: nothing -> nothing {
   let systemd_pre_env_abs_file_path = get-systemd-pre-env-abs-file-path
   let systemd_pre_env = open $systemd_pre_env_abs_file_path

   let systemd_post_env = systemctl --user show-environment
   | lines
   | split column '=' key value

   let systemd_env_diff = $systemd_pre_env | join -o $systemd_post_env key

   let env_name_todos = $systemd_env_diff | par-each {|row|
      match [$row.value $row.value_] {
         [null _] => { {unset: $row.key} }
         [_ null] => { }
         [_ _] => { {update: $row.key} }
      }
   }

   let systemd_env_names_to_unset = $env_name_todos.unset? | compact
   let systemd_env_names_to_update = $env_name_todos.update? | compact

   if $systemd_env_names_to_unset != null and ($systemd_env_names_to_unset | is-not-empty) {
      with-env (
         $systemd_env_names_to_unset | reduce -f {} {|systemd_env_name_to_unset acc|
            $acc | merge {$systemd_env_name_to_unset: ''}
         }
      ) {
         dbus-update-activation-environment ...$systemd_env_names_to_unset
      }

      systemctl --user unset-environment ...$systemd_env_names_to_unset
   }

   if $systemd_env_names_to_update != null and ($systemd_env_names_to_update | is-not-empty) {
      systemctl --user import-environment ...$systemd_env_names_to_update
      dbus-update-activation-environment ...$systemd_env_names_to_update
   }

   rm $systemd_pre_env_abs_file_path
}

# Does autostart that is defined in script.
def 'main autostart' [
] {
   (
      run-single-instance-per-user
      --process-name=io.elementary.desktop.agent-polkit
      '/usr/lib/policykit-1-pantheon/io.elementary.desktop.agent-polkit'
   )

   run-single-instance-per-user hypridle
   run-single-instance-per-user wayland-pipewire-idle-inhibit
   run-single-instance-per-user keepassxc
}

def run-single-instance-per-user [
   --user: string
   --systemd-wrapper-command: list<string> = ['runapp']
   --process-name: string
   ...command: string
]: nothing -> nothing {
   let process_name = if ($process_name == null) {
      $command.0
   } else {
      $process_name
   }

   let user = if ($user == null) {
      $env.LOGNAME
   } else {
      $user
   }

   let found_processes = ps -l | where {|process|
      let user = id -nu $process.user_id
      $process.name == $process_name and $user == $env.LOGNAME
   }

  if ($found_processes | is-empty) {
      try {
         run-external ...$systemd_wrapper_command ...$command
      } catch {|error|
         $error | print
      }
   }
}

def get-wm-systemd-service-name []: nothing -> string {
   'niri-custom.service'
}

def get-wm-env []: nothing -> record {
   {
      DISPLAY: ':0' # also set niri's config

      SDL_VIDEODRIVER: 'wayland,x11'
      CLUTTER_BACKEND: 'wayland'
      QT_QPA_PLATFORM: 'wayland;xcb'

      QT_QPA_PLATFORMTHEME: 'qt6ct'

      _JAVA_AWT_WM_NONREPARENTING: '1'
   }
}

def get-systemd-pre-env-abs-file-path []: nothing -> string {
   get-script-runtime-dir | path join 'systemd-pre-env.json'
}

def get-script-runtime-dir []: nothing -> string {
   $env.XDG_RUNTIME_DIR | path join 'niri-custom'
}
