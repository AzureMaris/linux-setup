#!/usr/bin/env nu

def run-single-instance-per-user [
   --user: string
   --detect-systemd = true
   --systemd-integrator-command: list<string>
   --process-name: string
   ...command: string
] {
   let process_name = if ($process_name == null) {
      $command.0
   } else {
      $process_name
   }

   let user = if ($user == null) {
      $env.LOGNAME
   } else {
      $user
   }

   let use_systemctl = if ($detect_systemd == false) {
      false
   } else {
      if (which systemctl | is-not-empty) {
         true
      } else {
         false
      }
   }

   ps -l | where {|process|
      $process.name == $process_name and (id -nu $process.user_id) == $env.LOGNAME
   } | if ($in | is-empty) {
      if $use_systemctl {
         if ($systemd_integrator_command | is-not-empty) {
            run-external ...$systemd_integrator_command ...$command
         } else {
            runapp ...$command
         }
      } else {
         run-external ...$command
      }
   }
}

do {
   let systemctl_exists = which systemctl | is-not-empty
   let uwsm_exists = which uwsm | is-not-empty

   if $systemctl_exists and $uwsm_exists {
      systemctl --user cat wayland-wm@niri.service
      | complete
      | if ($in.exit_code > 0) {
         uwsm start -U=home -o -- niri --session
      } else {
         systemctl --user start wayland-wm@niri.service
      }

      loop {
         systemctl --user is-active --quiet wayland-wm@niri.service
         | complete
         | if ($in.exit_code == 0) {
            break
         }

         sleep 0.5sec
      }
   } else {
      let job_id = job spawn {
         niri --session e>| lines | take until {|line|
            let should_detach = $line | str contains 'IPC listening on:'

            if not $should_detach {
               return false
            }

            # job disown $id # this is not yet implemented upstream
            $should_detach | job send 0
            $should_detach
         }
      }

      job recv
   }

   with-env {
      DISPLAY: 0
   } {
      (
         systemctl
         --user
         import-environment
         DISPLAY
         SDL_VIDEODRIVER
         CLUTTER_BACKEND
         QT_QPA_PLATFORM
         QT_QPA_PLATFORMTHEME
         _JAVA_AWT_WM_NONREPARENTING
      )
   }

   systemctl --user start xwayland-satellite.service

   (
      run-single-instance-per-user
      --process-name=io.elementary.desktop.agent-polkit
      '/usr/lib/policykit-1-pantheon/io.elementary.desktop.agent-polkit'
   )

   (
      run-single-instance-per-user
      --process-name=qs
      --systemd-integrator-command=['runapp' '-i' 'session-graphical.slice']
      'qs' '-c' 'noctalia-shell'
   )

   run-single-instance-per-user hypridle

   run-single-instance-per-user wayland-pipewire-idle-inhibit
   run-single-instance-per-user keepassxc
}
