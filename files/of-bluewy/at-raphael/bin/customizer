#!/usr/bin/env nu

use std log

def --wrapped main [
   wrapper_name: string # The script that it should find.
   ...command # The actual command.
]: nothing -> nothing {
   let wrapper_file_abs_path = get-config-dir-abs-path | path join $wrapper_name

   if ($wrapper_file_abs_path | path exists) {
      exec $wrapper_file_abs_path ...$command
      return
   }

   log info $"wrapper_file_abs_path=($wrapper_file_abs_path) not found"

   do {
      if (which dbus-send | is-empty) {
         return
      }

      let has_notification_service = (
         dbus-send
         --session
         --type=method_call
         --print-reply
         --dest=org.freedesktop.Notifications
         /org/freedesktop/Notifications
         org.freedesktop.Notifications.GetServerInformation
         | complete
         | get exit_code
         | $in <= 0
      )

      if not $has_notification_service {
         return
      }

      (
         notify-send
         ($env.CURRENT_FILE | path parse | get stem)
         $"wrapper_file_abs_path=($wrapper_file_abs_path) not found"
      )
   }
}

def get-config-dir-abs-path []: nothing -> string {
   $env.HOME | path join '.config' 'customizer'
}
