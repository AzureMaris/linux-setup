#!/usr/bin/env nu

use std log

def --wrapped main [
   wrapper_name: string # The script name it should find.
   ...rest # The command that is being wrapped.
]: nothing -> nothing {
   let wrapper_file_path = get-config-dir-path | path join $wrapper_name

   if ($wrapper_file_path | path exists) {
      exec $wrapper_file_path ...$rest
   }

   let error_log = $"not found: wrapper_file_path=($wrapper_file_path)"
   log error $error_log
   let notification_title = $env.CURRENT_FILE | path basename

   try {
      notify-send $notification_title $error_log
   } catch {
      log error "command failed: notify-send"
   }
}

def get-config-dir-path []: nothing -> string {
   let xdg_config_dir_path = $env.XDG_CONFIG_HOME?

   let parent_config_dir_path = if $xdg_config_dir_path != null {
      $xdg_config_dir_path
   } else {
      $env.HOME | path join .config
   }

   $parent_config_dir_path | path join 'customizer'
}
