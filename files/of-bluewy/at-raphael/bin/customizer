#!/usr/bin/env nu

use std log

def --wrapped main [
   wrapper_name: string # The script that it should find.
   ...command # The actual command.
]: nothing -> nothing {
   let wrapper_file_path = get-config-dir-path | path join $wrapper_name

   if ($wrapper_file_path | path exists) {
      exec $wrapper_file_path ...$command
   }

   log error $"wrapper_file_abs_path=($wrapper_file_path) not found"

   try {
      (
         notify-send
         ($env.CURRENT_FILE | path parse | get stem)
         $"wrapper_file_abs_path=($wrapper_file_path) not found"
      )
   } catch {
      log error "notify-send failed"
   }
}

def get-config-dir-path []: nothing -> string {
   let xdg_config_dir_path = $env.XDG_CONFIG_HOME?

   let parent_config_dir_path = if $xdg_config_dir_path != null {
      $xdg_config_dir_path
   } else {
      $env.HOME
   }

   $parent_config_dir_path | path join 'customizer'
}
